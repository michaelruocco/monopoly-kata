plugins {
    id 'java-library'
    id 'idea'
    id 'jacoco'
    id 'org.unbroken-dome.test-sets' version '3.0.1'
}

repositories {
    mavenCentral()
}

testSets {
    cucumberTest
}

configurations {
    cucumberTest.setCanBeResolved(true)
}

ext {
    lombokVersion = "1.18.20"

    junitVersion = "5.7.1"
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "org.slf4j:slf4j-api:1.7.30"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'org.mockito:mockito-core:3.8.0'

    cucumberTestCompile 'io.cucumber:cucumber-java:6.8.1'
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperty "org.slf4j.simpleLogger.defaultLogLevel", "debug"
    testLogging {
        events "failed"
        exceptionFormat "full"
        showStandardStreams true
    }
}

task cucumber() {
    dependsOn assemble, testClasses, cucumberTestClasses

    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberTestRuntime + configurations.runtime + sourceSets.main.output + sourceSets.cucumberTest.output
            args = ['--plugin', 'pretty', '--glue', 'uk.co.mruoc.monopoly', 'src/cucumberTest/resources']
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint'
}
