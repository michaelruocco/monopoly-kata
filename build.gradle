plugins {
    id 'java-library'
    id 'idea'
    id 'jacoco'
    id 'org.unbroken-dome.test-sets' version '3.0.1'
}

repositories {
    mavenCentral()
}

testSets {
    cucumberTest
}

configurations {
    cucumberTest.setCanBeResolved(true)
}

dependencies {
    compile 'log4j:log4j:1.2.17'

    testCompile 'org.assertj:assertj-core:3.5.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.2.15'

    cucumberTestCompile 'io.cucumber:cucumber-java:6.8.1'
}

task cucumber() {
    dependsOn assemble, testClasses, cucumberTestClasses

    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberTestRuntime + configurations.runtime + sourceSets.main.output + sourceSets.cucumberTest.output
            args = ['--plugin', 'pretty', '--glue', 'uk.co.mruoc.monopoly', 'src/cucumberTest/resources']
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint'
}
